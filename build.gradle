buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id "maven-publish"
    id 'java'
}


group 'io.iotchain'
version '1.0.0'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def siteUrl = "https://github.com/iot-block/iotchainj"
def gitUrl = "https://github.com/iot-block/iotchainj.git"

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "iotchain"
            name "iotchain"
            email "support@iotchain.io"
        }
    }

    scm {
        url siteUrl
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'io.iotchain'
            artifactId 'iotchainj'
            version '1.0.0'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'iotchain sdk')
                root.appendNode('name', 'iotchain')
                root.appendNode('url', siteUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']

    pkg {
        repo = 'iotchain'
        name = 'iotchainj'
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        publish = true
    }

}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
    compile group: 'org.web3j', name: 'abi', version: '4.5.5'
    compile group: 'org.web3j', name: 'rlp', version: '4.5.5'
    compile group: 'org.web3j', name: 'crypto', version: '4.5.5'
    compile group: 'org.web3j', name: 'utils', version: '4.5.5'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.9.0'
    compile group: 'com.alibaba', name: 'fastjson', version: '1.2.62'

}
