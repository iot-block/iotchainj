
plugins {
    id 'java'
    id "maven-publish"
    id "signing"
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
    compile group: 'org.web3j', name: 'abi', version: '4.5.5'
    compile group: 'org.web3j', name: 'rlp', version: '4.5.5'
    compile group: 'org.web3j', name: 'crypto', version: '4.5.5'
    compile group: 'org.web3j', name: 'utils', version: '4.5.5'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.9.0'
    compile group: 'com.alibaba', name: 'fastjson', version: '1.2.62'
}

test {
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'io.iotchain'
            artifactId 'iotchainj'
            version '1.0.0'

            from components.java

            // create the sign pom artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'IoT Chain SDK'
            name 'iotchain'
            url 'https://github.com/iot-block/iotchainj'
            organization {
                name 'iotchain'
                url 'https://github.com/iot-block'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/iot-block/iotchainj/issues'
            }
            licenses {
                license {
                    name 'Apache License 2.0'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/iot-block/iotchainj'
                connection 'scm:git:git://github.com/iot-block/iotchainj.git'
                developerConnection 'scm:git:ssh://git@github.com:iot-block/iotchainj.git'
            }
            developers {
                developer {
                    name 'iotchain'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}